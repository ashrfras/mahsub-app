ئورد عنصرمخصص، مكون، عنصرئشتمل؛
ئورد مشهد.رئسئدارة؛
ئورد المستند من مستند؛
ئورد مكون.طافية.طافية؛
ئورد الكل من صنفمجال.ئتيلات؛
ئورد مختطمتجر من صنفمجال.متجر؛
ئورد الكل ك مجالمتجر من صنفمجال.متجر؛
ئورد عنصرئدخال؛
ئورد مكون.غمازة؛
ئورد مكون.حقلئدخ؛
ئورد مشهد.طافية.طفنشرة؛
ئورد مشهد.طافية.طفنسخة؛
ئورد عنصرتحديد؛
ئعلن ئعداد ()؛
ئعلن ئعداد.نقرحفضعام () ك وعد؛
ئعلن ئعداد.نقرتئكيدضبطمصنع () ك وعد؛
ئعلن ئعداد.نقرضبطمصنع ()؛
ئعلن ئعداد.حممل () ك وعد؛
ئعلن ئعداد.صيير ()؛
ئعلن ئعداد.نقرحفضفاتورة () ك وعد؛
ئعلن ئعداد.نقرمعلونسخة ()؛
ئعلن مدخل ()؛
ئعلن ئعداد ():
	يمدد مكون()؛
	يملك مركب {مختطمتجر متجر} حالة = {متجر: عدم}؛
تم

ئعلن ئعداد.نقرحفضعام () ك وعد:
	غمازة غمز = المستند.ئستعلم((<غمازة>))؛
	عنصرئشتمل قسمعام = هدا.ئستعلم('#قسمعام')؛
	حقلئدخ[] ئدخا = قسمعام.ئستعلمكل((<حقلئدخ>))؛
	عنصرتحديد ئدخنشاط = قسمعام.ئستعلم((<تحديد>))؛
	
	منطق هلتحقق = صحيح؛
	لكل ئدخ في ئدخا:
		هلتحقق = ئدخ.ئفحص() وو هلتحقق؛
	تم
	ئدا هلتحقق:
		لكل ئدخ في ئدخا:
			هدا.حالة.متجر[ئدخ.ئسم] = ئدخ.ردقيمة()؛
		تم
		هدا.حالة.متجر.نشاطتجاري = ئدخنشاط.قيمة؛
		ريتما مجالمتجر.ئحفض(هدا.حالة.متجر)؛
		غمز.خدحالة({
			رسالة: 'تم حفظ التعديلات'
		})؛
	تم
تم

ئعلن ئعداد.نقرتئكيدضبطمصنع () ك وعد:
	ريتما ئتيلات.ئحدفقاعدب()؛
	المستند.موقع.ئعدتحميل()؛
تم
ئعلن ئعداد.نقرضبطمصنع ():
	طافية طفنعلا = المستند.ئستعلم('#طفنعملا')؛
	طفنعلا.خدحالة({
		جسم: 'طفنعملا'،
		بيا: {
			رسالة: 'سيتم حذف جميع بياناتك ومبيعاتك من هذا الجهاز. هل أنت متأكد؟'،
			عندلا: (دالة(): طفنعلا.ئخفي())،
			عندنعم: هدا.نقرتئكيدضبطمصنع،
			مرجع: هدا
		}
	})؛
تم

ئعلن ئعداد.حممل () ك وعد:
	هدا.ئربطحدتين()؛
	هدا.خدحالة({
		متجر: ريتما مجالمتجر.ردمتجر()
	})؛
	عنصرئشتمل قسمعام = هدا.ئستعلم('#قسمعام')؛
	عنصرتحديد ئدخنشاط = قسمعام.ئستعلم((<تحديد>))؛
	ئدخنشاط.قيمة = هدا.حالة.متجر.نشاطتجاري؛
تم

ئعلن ئعداد.صيير ():
	ئرجع (
		<طافية معرف="طفئتفاقية" حجم="كبير" عنصر-بطاقة="مقوسكب:هام موضع-مطلق فوق-2"></طافية>
		<طافية معرف="طفنسخة" حجم="كبير" عنصر-بطاقة="مقوسكب:هام موضع-مطلق فوق-2"></طافية>
		<قسم صنف="مخطط-طولي خلف-رمادي-200 ئدنط-كامل">
			<رئسئدارة صفحة="ئعداد"></رئسئدارة>
			<قسم صنف="مخطط-طولي فجوة-4 حش-5 حشفو-3">
				<شرط>
					<عند تحقق="هدا.حالة.متجر">

						<قسم صنف="مخطط-طولي فجوة-2">
							<حيز صنف="خطسميك">إعدادات عامة</حيز>
							<قسم معرف="قسمعام" صنف="مخطط-طولي فجوة-3 خلف-ئبيض حش-4 مقوسكب">
								<حقلئدخ معطل ملصق="رمز المتجر" قيمة="%{هدا.حالة.متجر.رمزمتجر ئو ''}"></حقلئدخ>
								<حقلئدخ ئجباري ئسم="ئسمتجر" ملصق="اسم المتجر" قيمة="%{هدا.حالة.متجر.ئسمتجر ئو ''}" ستنب="مثلا: الخير للتجارة والتسيير"></حقلئدخ>

								<قسم صنف="مخطط-طولي">
									<حيز صنف="نصصغ">النشاط التجاري</حيز>
									<تحديد ئسم="نشاطتجاري" صنف="ط-12 خلف-ئبيض مقوس ئطار-1 ئطار-رمادي-400">
										<خيار قيمة="مقاهي ومطاعم">مقاهي ومطاعم</خيار>
										<خيار قيمة="عطور ومستحضرات">عطور ومستحضرات</خيار>
										<خيار قيمة="أنشطة أخرى">أنشطة أخرى</خيار>
									</تحديد>
								</قسم>

								<حقلئدخ ئسم="رمزعملة" ملصق="رمز العملة" قيمة="%{هدا.حالة.متجر.رمزعملة ئو ''}" ستنب="مثلا: درهم"></حقلئدخ>
								<قسم صنف="مخطط ئرصف-نهاية نفسه-نهاية">
									<زرئدخ عندنقر="نقرحفضعام" ملصق="حفظ" صنف="ع-16"></زرئدخ>
								</قسم>
							</قسم>
						</قسم>

						<قسم صنف="مخطط-طولي فجوة-2">
							<حيز صنف="خطسميك">إعدادات الفاتورة</حيز>
							<قسم معرف="قسمفاتورة" صنف="مخطط-طولي فجوة-3 خلف-ئبيض حش-4 مقوسكب">
								<حقلئدخ ئجباري ئسم="ئسمتجاري" ملصق="الاسم التجاري" قيمة="%{هدا.حالة.متجر.ئسمتجاري ئو ''}" ستنب=""></حقلئدخ>
								<حقلئدخ ئسم="هاتف" نوع="هاتف" ملصق="الهاتف" قيمة="%{هدا.حالة.متجر.هاتف ئو ''}" ستنب="+000 000 000 000"></حقلئدخ>
								<حقلئدخ ئسم="بريدرقمي" نوع="بريد" ملصق="البريد الرقمي" قيمة="%{هدا.حالة.متجر.بريدرقمي ئو ''}" ستنب="info@example.com"></حقلئدخ>
								<حقلئدخ ئسم="رابط" نوع="رابط" ملصق="الرابط" قيمة="%{هدا.حالة.متجر.رابط ئو ''}" ستنب="www.mahsub.app"></حقلئدخ>
								<حقلئدخ ئسم="تدييل" ملصق="رسالة التذييل" قيمة="%{هدا.حالة.متجر.تدييل ئو ''}" ستنب="شكرا على زيارتكم"></حقلئدخ>
								<قسم صنف="مخطط ئرصف-نهاية نفسه-نهاية">
									<زرئدخ عندنقر="نقرحفضفاتورة" ملصق="حفظ" صنف="ع-16"></زرئدخ>
								</قسم>
							</قسم>
						</قسم>

						<قسم صنف="مخطط-طولي فجوة-2">
							<حيز صنف="خطسميك">حول البرنامج</حيز>
							<قسم صنف="مخطط-طولي فجوة-3 خلف-ئبيض حش-4 مقوسكب">
								<قسم صنف="مخطط-طولي فجوة-2">
									<حيز عندنقر="نقرمعلونسخة" صنف="تئشير:شرشير-500 مئشر-سبابة">معلومات النسخة</حيز>
									<حيز عندنقر="نقرئتفاقية" صنف="تئشير:شرشير-500 مئشر-سبابة">اتفاقية الاستخدام</حيز>
									<حيز عندنقر="نقرضبطمصنع" صنف="تئشير:شرشير-500 مئشر-سبابة">ضبط المصنع</حيز>
								</قسم>
							</قسم>
						</قسم>

					</عند>
				</شرط>
			</قسم>
		</قسم>
	)؛
تم

ئعلن ئعداد.نقرحفضفاتورة () ك وعد:
	غمازة غمز = المستند.ئستعلم((<غمازة>))؛
	عنصرئشتمل قسمفاتورة = هدا.ئستعلم('#قسمفاتورة')؛
	حقلئدخ[] ئدخا = قسمفاتورة.ئستعلمكل((<حقلئدخ>))؛
	منطق هلتحقق = صحيح؛
	لكل ئدخ في ئدخا:
		هلتحقق = ئدخ.ئفحص() وو هلتحقق؛
	تم
	ئدا هلتحقق:
		لكل ئدخ في ئدخا:
			هدا.حالة.متجر[ئدخ.ئسم] = ئدخ.ردقيمة()؛
		تم
		ريتما مجالمتجر.ئحفض(هدا.حالة.متجر)؛
		غمز.خدحالة({
			رسالة: 'تم حفظ التعديلات'
		})؛
	تم
تم
ئعلن ئعداد.نقرئتفاقية ():
	طافية طفخ = المستند.ئستعلم('#طفئتفاقية')؛
	طفخ.خدحالة({
		جسم: 'طفنشرة'
	})؛
تم
ئعلن ئعداد.نقرمعلونسخة ():
	طافية طفخ = المستند.ئستعلم('#طفنسخة')؛
	طفخ.خدحالة({
		جسم: 'طفنسخة'
	})؛
تم
ئعلن مدخل ():
	عنصرمخصص.سججل('ئعداد'، ئعداد)؛
تم
