ئورد عنصرمخصص، مكون، عنصرئشتمل؛
ئورد مشهد.رئسئدارة؛
ئورد المستند من مستند؛
ئورد مكون.طافية.طافية؛
ئورد الكل من صنفمجال.ئتيلات؛
ئعلن ئعداد ()؛
ئعلن ئعداد.نقرتئكيدضبطمصنع () ك وعد؛
ئعلن ئعداد.نقرضبطمصنع ()؛
ئعلن ئعداد.صيير ()؛
ئعلن مدخل ()؛
ئعلن ئعداد ():
	يمدد مكون()؛
تم

ئعلن ئعداد.نقرتئكيدضبطمصنع () ك وعد:
	ريتما ئتيلات.ئحدفقاعدب()؛
	المستند.موقع.ئعدتحميل()؛
تم
ئعلن ئعداد.نقرضبطمصنع ():
	طافية طفنعلا = المستند.ئستعلم('#طفنعملا')؛
	طفنعلا.خدحالة({
		جسم: 'طفنعملا'،
		بيا: {
			رسالة: 'سيتم حذف جميع بياناتك ومبيعاتك من هذا الجهاز. هل أنت متأكد؟'،
			عندلا: (دالة(): طفنعلا.ئخفي())،
			عندنعم: هدا.نقرتئكيدضبطمصنع،
			مرجع: هدا
		}
	})؛
تم

ئعلن ئعداد.صيير ():
	ئرجع (
		<قسم صنف="مخطط-طولي خلف-رمادي-200 ئدنط-كامل">
			<رئسئدارة صفحة="ئعداد"></رئسئدارة>
			<قسم صنف="مخطط-طولي فجوة-4 حش-5 حشفو-3">

				<قسم صنف="مخطط-طولي فجوة-2">
					<حيز صنف="خطسميك">إعدادات عامة</حيز>
					<قسم صنف="مخطط-طولي فجوة-3 خلف-ئبيض حش-4 مقوسكب">
						<حقلئدخ ملصق="رمز المتجر" ستنب=""></حقلئدخ>
						<حقلئدخ ملصق="اسم الشركة" ستنب="مثلا: الخير للتجارة والتسيير"></حقلئدخ>
						<حقلئدخ ملصق="رمز العملة" ستنب="مثلا: درهم"></حقلئدخ>
						<قسم صنف="مخطط ئرصف-نهاية نفسه-نهاية">
							<زرئدخ ملصق="حفظ" صنف="ع-16"></زرئدخ>
						</قسم>
					</قسم>
				</قسم>

				<قسم صنف="مخطط-طولي فجوة-2">
					<حيز صنف="خطسميك">إعدادات الفاتورة</حيز>
					<قسم صنف="مخطط-طولي فجوة-3 خلف-ئبيض حش-4 مقوسكب">
						<حقلئدخ ملصق="الاسم التجاري" ستنب=""></حقلئدخ>
						<حقلئدخ ملصق="الهاتف" ستنب="+000 000 000 000"></حقلئدخ>
						<حقلئدخ ملصق="البريد الرقمي" ستنب="info@example.com"></حقلئدخ>
						<حقلئدخ ملصق="الرابط" ستنب="www.mahsub.app"></حقلئدخ>
						<حقلئدخ ملصق="رسالة التذييل" ستنب="شكرا على زيارتكم"></حقلئدخ>
						<قسم صنف="مخطط ئرصف-نهاية نفسه-نهاية">
							<زرئدخ ملصق="حفظ" صنف="ع-16"></زرئدخ>
						</قسم>
					</قسم>
				</قسم>

				<قسم صنف="مخطط-طولي فجوة-2">
					<حيز صنف="خطسميك">حول البرنامج</حيز>
					<قسم صنف="مخطط-طولي فجوة-3 خلف-ئبيض حش-4 مقوسكب">
						<قسم صنف="مخطط-طولي فجوة-2">
							<حيز صنف="تئشير:شرشير-500 مئشر-سبابة">معلومات النسخة</حيز>
							<حيز صنف="تئشير:شرشير-500 مئشر-سبابة">اتفاقية الاستخدام</حيز>
							<حيز عندنقر="نقرضبطمصنع" صنف="تئشير:شرشير-500 مئشر-سبابة">ضبط المصنع</حيز>
						</قسم>
					</قسم>
				</قسم>

			</قسم>
		</قسم>
	)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('ئعداد'، ئعداد)؛
تم
