ئورد عنصرمخصص، عنصرئشتمل، مكون؛
ئورد مجال من مصفوفة؛
ئورد مشهد.عنصر.عنصرمخزون؛
ئورد الكل من تنسيق؛
ئورد التطبيق من تطبيق؛
ئورد مشهد.طافية.طفمنتج؛
ئورد مشهد.ئيقوزائد؛
ئورد مكون.طافية.طافية؛
ئورد مشهد.رئسئدارة؛
ئورد مختطمنتج من صنفمجال.منتج؛
ئورد الكل ك مجالمنتج من صنفمجال.منتج؛
ئعلن مخزون ()؛
ئعلن مخزون.صيير ()؛
ئعلن مخزون.نقرزائدمنتج (عنصرمخزون عنص)؛
ئعلن مخزون.حددت () ك وعد؛
ئعلن مخزون.حممل () ك وعد؛
ئعلن مدخل ()؛
ئعلن مخزون ():
	يمدد مكون()؛
	
	يملك مركب {
		نصية فلتر
	} حالة = {
		فلتر: ''
	}؛
	
	يملك مختطمنتج[] منتجين = []؛
	
تم

ئعلن مخزون.صيير ():
	مختطمنتج منت؛
	ئرجع (
		<طافية معرف="طفتعديلمنتج" حجم="كبير" عنصر-بطاقة="مقوسكب:هام حش-0:هام هش-0:هام موضع-مطلق فوق-2"></طافية>
		<طافية معرف="طفخيا" عنصر-بطاقة="ئدنط-32:هام ط-32 مقوس"></طافية>
		<ئيقوزائد عندنقر="نقرزائدمنتج"></ئيقوزائد>
		<قسم صنف="مخطط-طولي خلف-رمادي-200" سمة="height:100dvh">
			<رئسئدارة صفحة="مخزون"></رئسئدارة>
			<قسم صنف="هش-2">
				<شرط>
					<عند تحقق="هدا.منتجين.طول">
						<ئدخال صنف="%{تنسيق.ئدخال} ع-كامل حش-2" ستنب="بحث"></ئدخال>
					</عند>
				</شرط>
			</قسم>
			<قسم صنف="خلف-رمادي-200 يكبر فائض-تلقا فائض-لمسي هش-2">
				<شرط>
					<عند تحقق="هدا.منتجين.طول">
						<شبكي فجوة="14 14" عموصغ="2" عموسط="2">
							<تكرار لكل="منت" في="هدا.منتجين">
								<عنصرمخزون معرفمنتج="%{منت._معرف}" ئسمنتج="%{منت.ئسمنتج}" سعر="%{منت.سعر}"></عنصرمخزون>
							</تكرار>
						</شبكي>
					</عند>
					<عند غيره>
						<قسم صنف="مخطط ط-ئلاربع ع-كامل ئصفف-وسط ئرصف-وسط">
							<حيز>اضغط \(+\) لإضافة منتجات</حيز>
						</قسم>
					</عند>
				</شرط>
			</قسم>
		</قسم>
	)؛
تم

ئعلن مخزون.نقرزائدمنتج (عنصرمخزون عنص):
	طافية طف = هدا.ئستعلم('#طفتعديلمنتج')؛
	طف.خدحالة({
		جسم: 'طفمنتج'،
		بيا: {
			معرفمنتج: عدم،
			مكونتحديت: هدا
		}
	})؛
تم

ئعلن مخزون.حددت () ك وعد:
	ئدا هدا.حالة.فلتر.يتضمن('تصنيف:'):
		نصية تصني = هدا.حالة.فلتر.عووض('تصنيف:'، '')؛
		هدا.منتجين = ريتما مجالمنتج.ردحيت(تصنيف: تصني)؛
	وئلا:
		هدا.منتجين = ريتما مجالمنتج.ردحيت(مبحت: هدا.حالة.فلتر)؛
	تم
	هدا.حددتمشهد()؛
	هدا.ئربطحدتين()؛
تم

ئعلن مخزون.حممل () ك وعد:
	هدا.ئربطحدتين()؛
	هدا.خدحالة({
		فلتر: ''
	})؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مخزون'، مخزون)؛
تم
