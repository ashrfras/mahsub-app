ئورد مكون، عنصرمخصص، عنصرئشتمل؛
ئورد التطبيق من تطبيق؛
ئورد مجال من مصفوفة؛
ئورد مشهد.رئسئدارة؛
ئورد الكل ك مجالمستخدم من صنفمجال.مستخدم؛
ئورد مختطمستخدم من صنفمجال.مستخدم؛
ئورد مشهد.عنصر.عنصرمستخدم؛
ئورد مكون.طافية.طافية؛
ئورد مشهد.طافية.طفمستخدم؛
ئعلن مستخدمين ()؛
ئعلن مستخدمين.نقرزائدمستخدم (عنصرمستخدم عنص)؛
ئعلن مستخدمين.حممل () ك وعد؛
ئعلن مستخدمين.صيير ()؛
ئعلن مدخل ()؛
ئعلن مستخدمين ():
	يمدد مكون()؛
	يملك مركب { مختطمستخدم[] مستخدمين } حالة = {
		مستخدمين: []
	}؛
تم

ئعلن مستخدمين.نقرزائدمستخدم (عنصرمستخدم عنص):
	طافية طف = هدا.ئستعلم('#طفتعديلمستخدم')؛
	طف.خدحالة({
		جسم: 'طفمستخدم'،
		بيا: {
			معرفمستخدم: عدم،
			مكونتحديت: هدا
		}
	})؛
تم

ئعلن مستخدمين.حممل () ك وعد:
	هدا.ئربطحدتين()؛
	هدا.خدحالة({
		مستخدمين: ريتما مجالمستخدم.ردجميع()
	})؛
تم

ئعلن مستخدمين.صيير ():
	مختطمستخدم مست؛
	ئرجع (
		<طافية معرف="طفتعديلمستخدم" حجم="كبير" عنصر-بطاقة="مقوسكب:هام حش-0:هام هش-0:هام موضع-مطلق فوق-2"></طافية>
		<طافية معرف="طفخيا" صفحة="مستخدمين" عنصر-بطاقة="ئدنط-20:هام ط-20 مقوس"></طافية>
		<ئيقوزائد عندنقر="نقرزائدمستخدم"></ئيقوزائد>
		<قسم صنف="مخطط-طولي خلف-رمادي-200" سمة="height:100dvh">
			<رئسئدارة صفحة="مخزون"></رئسئدارة>
			<قسم صنف="هش-3 هشتح-1">
				<شرط>
					<عند تحقق="هدا.حالة.مستخدمين.طول">
						<حيز صنف="خطسميك">المستخدمون:</حيز>
					</عند>
				</شرط>
			</قسم>
			<قسم صنف="خلف-رمادي-200 يكبر فائض-تلقا فائض-لمسي هش-3">
				<شرط>
					<عند تحقق="هدا.حالة.مستخدمين.طول">
						<شبكي فجوة="12 12" عموصغ="2" عموسط="2">
							<تكرار لكل="مست" في="هدا.حالة.مستخدمين">
								<عنصرمستخدم معرفمستخدم="%{مست._معرف}" ئسمكامل="%{مست.ئسمكامل}" صلاحيات="%{مست.صلاحيات}"></عنصرمستخدم>
							</تكرار>
						</شبكي>
					</عند>
					<عند غيره>
						<قسم صنف="مخطط ط-ئلاربع ع-كامل ئصفف-وسط ئرصف-وسط">
							<حيز>اضغط \(+\) لإضافة مستخدمين</حيز>
						</قسم>
					</عند>
				</شرط>
			</قسم>
		</قسم>
	)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('مستخدمين'، مستخدمين)؛
تم