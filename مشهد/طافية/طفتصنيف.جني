ئورد عنصرمخصص، مكون، عنصرئشتمل، عنصرئدخال؛
ئورد الكل من تنسيق؛
ئورد مكون.طافية.طافية؛
ئورد المستند من مستند؛
ئورد مختطتصنيف من صنفمجال.تصنيف؛
ئورد الكل ك مجالتصنيف من صنفمجال.تصنيف؛
ئورد الكل من صنفمجال.ئتيلات؛
ئورد مكون.غمازة؛
ئعلن طفتصنيف ()؛
ئعلن طفتصنيف.صيير ()؛
ئعلن طفتصنيف.ئغلقطافية ()؛
ئعلن طفتصنيف.عندحفض () ك وعد؛
ئعلن طفتصنيف.حممل () ك وعد؛
ئعلن طفتصنيف.عندنقرئيقونة (عنصرئشتمل عنص)؛
ئعلن مدخل ()؛
ئعلن طفتصنيف ():
	يمدد مكون()؛
	يملك مركب {
		نصية معرفتصنيف
	} حالة = {
		معرفتصنيف: عدم
	}؛
	يملك مختطتصنيف تصنيف؛
	يملك نصية رمزمحدد؛
تم

ئعلن طفتصنيف.صيير ():
	نصية[] ئيقونات = ['شنطة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة'، 'قمامة']؛
	نصية قيمتئدخ = هدا.تصنيف.ئسمتصنيف ئدا هدا.تصنيف وئلا ''؛
	نصية ئيقحالي = هدا.تصنيف.رمزتصنيف ئدا هدا.تصنيف وئلا 'شنطة'؛
	هدا.رمزمحدد = ئيقحالي؛
	ئرجع (
		<قسم صنف="مخطط-طولي خلف-ئبيض مقوس هش-4 فجوة-6">
			<قسم صنف="مخطط-عرضي ئصفف-وسط هشفو-2">
				<قسم صنف="ع-ئربعون">اسم التصنيف:</قسم>
				<ئدخال قيمة="%{قيمتئدخ}" صنف="%{تنسيق.ئدخال} يملئ ط-8"></ئدخال>
			</قسم>
			<قسم صنف="مخطط-طولي فجوة-1">
				<حيز>اختر أيقونة التصنيف:</حيز>
				<قسم صنف="مخطط-عرضي فجوة-2 مقوس ئطار-1 ئطار-رمادي-400 حش-2 ئقصع-كامل ئقصط-مئتان مخطط-يلتف فائض-تلقا فائض-لمسي ئرصف-وسط ئصفف-وسط">
					<تكرار لكل="ئيق" في="ئيقونات">
						<شرط>
							<عند تحقق="ئيق == ئيقحالي">
								<ئيقو ئسم="%{ئيق}" صنف="خلف-رمادي-300 حش-2 مقوس نصكجج تئشير:خلف-شرشير-700 تئشير:ئبيض"></ئيقو>
							</عند>
							<عند غيره>
								<ئيقو ئسم="%{ئيق}" عندنقر="عندنقرئيقونة" صنف="حش-2 مقوس نصكجج تئشير:خلف-شرشير-700 تئشير:ئبيض"></ئيقو>
							</عند>
						</شرط>
					</تكرار>
				</قسم>
			</قسم>
			<قسم صنف="مخطط-عرضي فجوة-2">
				<زرئدخ عندنقر="ئغلقطافية" ملصق="إلغاء" صنف="يملئ يكبر" تانوي></زرئدخ>
				<زرئدخ عندنقر="عندحفض" ملصق="حفظ" صنف="يملئ يكبر"></زرئدخ>
			</قسم>
		</قسم>
	)؛
تم

ئعلن طفتصنيف.ئغلقطافية ():
	طافية طف = المستند.ئستعلم('#طفتعديلتصنيف')؛
	طف.ئخفي()؛
تم

ئعلن طفتصنيف.عندحفض () ك وعد:
	عنصرئدخال ئدخ = هدا.ئستعلم((<ئدخال>))؛
	نصية قيمة = ئدخ.قيمة؛
	ئدا قيمة == '':
		غمازة غمز = المستند.ئستعلم((<غمازة>))؛
		غمز.خدحالة({
			رسالة: 'يرجى إدخال اسم التصنيف'
		})؛
		قل 'غمازة'؛
		ئرجع؛
	تم
	مختطتصنيف تصنف = هدا.تصنيف؛
	نصية رمز = هدا.رمزمحدد؛
	ئدا تصنف:
		#نحن نعددل تصنيفا موجودا؛
		تصنف.ئسمتصنيف = قيمة؛
		تصنف.رمزتصنيف = رمز؛
	وئلا:
		#نحن نضيف تصنيفا جديدا؛
		تصنف = {
			ئسمتصنيف: قيمة،
			رمزتصنيف: رمز
		}؛
	تم
	ريتما مجالتصنيف.ئحفض(تصنف)؛
	هدا.ئغلقطافية()؛
تم
ئعلن طفتصنيف.حممل () ك وعد:
	ئدا هدا.حالة.معرفتصنيف:
		هدا.تصنيف = ريتما مجالتصنيف.ردبمعرف(هدا.حالة.معرفتصنيف)؛
	تم
	هدا.ئربطحدتين()؛
تم
ئعلن طفتصنيف.عندنقرئيقونة (عنصرئشتمل عنص):
	هدا.رمزمحدد = عنص.ردخاصية('ئسم')؛
تم
ئعلن مدخل ():
	عنصرمخصص.سججل('طفتصنيف'، طفتصنيف)؛
تم
