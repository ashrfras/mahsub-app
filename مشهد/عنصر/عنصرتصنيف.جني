ئورد عنصرمخصص، مكون، عنصرئشتمل؛
ئورد وضيفة؛
ئورد مشهد.طافية.طفخيا؛
ئورد مكون.طافية.طافية؛
ئورد المستند من مستند؛
ئورد مشهد.طافية.طفنعملا؛
ئورد مكون.غمازة.غمازة؛
ئورد الكل ك مجالتصنيف من صنفمجال.تصنيف؛
ئعلن عنصرتصنيف ()؛
ئعلن عنصرتصنيف.نقرخيا ()؛
ئعلن عنصرتصنيف._نقرخياحدف()؛
ئعلن عنصرتصنيف._نقرخياتعديل ()؛
ئعلن عنصرتصنيف.صيير ()؛
ئعلن عنصرتصنيف.نقرتئكيدحدف () ك وعد؛
ئعلن مدخل ()؛
ئعلن عنصرتصنيف ():
	يمدد مكون()؛
	يملك مركب {
		نصية معرفتصنيف،
		نصية ئسمتصنيف،
		نصية رمزتصنيف
	} حالة = {
		معرفتصنيف: هدا.ردخاصية('معرفتصنيف')،
		ئسمتصنيف: هدا.ردخاصية('ئسم')،
		رمزتصنيف: هدا.ردخاصية('رمز')
	}؛
	يملك وضيفة نقرخياتعديل = (هدا._نقرخياتعديل ك وضيفة).ئربط(هدا)؛
	يملك وضيفة نقرخياحدف = (هدا._نقرخياحدف ك وضيفة).ئربط(هدا)؛
تم

ئعلن عنصرتصنيف.نقرخيا ():
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.خدحالة({
		جسم: 'طفخيا'
	})؛
	طفخ.ئضفمستمعحدت('نقرتعديل'، هدا.نقرخياتعديل)؛
	طفخ.ئضفمستمعحدت('نقرحدف'، هدا.نقرخياحدف)؛
تم

ئعلن عنصرتصنيف._نقرخياحدف():
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.ئحدفمستمعحدت('نقرحدف'، هدا.نقرخياحدف)؛
	طفخ.ئخفي()؛
	
	طافية طفنعلا = المستند.ئستعلم('#طفنعملا')؛
	طفنعلا.خدحالة({
		جسم: 'طفنعملا'،
		بيا: {
			رسالة: 'هل أنت متأكد من رغبتك في حذف هذا التصنيف؟'،
			عندلا: (دالة(): طفنعلا.ئخفي())،
			عندنعم: هدا.نقرتئكيدحدف،
			مرجع: هدا
		}
	})؛
تم

ئعلن عنصرتصنيف._نقرخياتعديل ():
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.ئحدفمستمعحدت('نقرتعديل'، هدا.نقرخياتعديل)؛
	طفخ.ئخفي()؛
	طافية طف = المستند.ئستعلم('#طفتعديلتصنيف')؛
	طف.خدحالة({
		جسم: 'طفتصنيف'،
		بيا: {
			معرفتصنيف: هدا.حالة.معرفتصنيف،
			عنصرتصن: هدا
		}
	})؛
تم

ئعلن عنصرتصنيف.صيير ():
	ئرجع (
		<قسم صنف="تئشير:ئطار-ئسود ئطار-2 ئطار-ئبيض خلف-ئبيض مقوس مخطط-طولي فجوة-2 ئرصف-وسط حش-2">
			<قسم صنف="مخطط-عرضي ئرصف-بين ئصفف-وسط">
				<قسم صنف="فجوة-1 مخطط-طولي">
					<ئيقو صنف="نصكجج" ئسم="%{هدا.حالة.رمزتصنيف}"></ئيقو>
					<حيز صنف="">%{هدا.حالة.ئسمتصنيف}</حيز>
				</قسم>
				<ئيقو عندنقر="نقرخيا" ئسم="تلاتنقط-ئفقي-دائرة" صنف="نصكجج حيادي-700"></ئيقو>
			</قسم>
		</قسم>
	)؛
تم

ئعلن عنصرتصنيف.نقرتئكيدحدف () ك وعد:
	ريتما مجالتصنيف.ئحدفبمعرف(هدا.حالة.معرفتصنيف)؛
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.ئخفي()؛
	
	غمازة غمز = المستند.ئستعلم((<غمازة>))؛
	غمز.خدحالة({
		رسالة: 'تم حذف التصنيف'
	})؛
	هدا.ئسمصنف = هدا.ئسمصنف + ' بلاعرض:هام'؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('عنصرتصنيف'، عنصرتصنيف)؛
تم
