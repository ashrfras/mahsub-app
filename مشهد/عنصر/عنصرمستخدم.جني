ئورد عنصرمخصص، مكون، عنصرئشتمل؛
ئورد وضيفة؛
ئورد المستند من مستند؛
ئورد مكون.طافية.طافية؛
ئورد مشهد.طافية.طفخيا؛
ئورد الكل ك مجالمستخدم من صنفمجال.مستخدم؛
ئورد مختطمستخدم من صنفمجال.مستخدم؛
ئورد مكون.غمازة؛
ئعلن عنصرمستخدم ()؛
ئعلن عنصرمستخدم.نقرخيا ()؛
ئعلن عنصرمستخدم._نقرخياحدف()؛
ئعلن عنصرمستخدم.نقرتئكيدحدف () ك وعد؛
ئعلن عنصرمستخدم._نقرخياتعديل ()؛
ئعلن عنصرمستخدم.صيير ()؛
ئعلن مدخل ()؛
ئعلن عنصرمستخدم ():
	يمدد مكون()؛
	يملك مركب {
		نصية معرفمستخدم،
		نصية ئسمكامل،
		نصية صلاحيات
	} حالة = {
		معرفمستخدم: هدا.ردخاصية('معرفمستخدم')،
		ئسمكامل: هدا.ردخاصية('ئسمكامل')،
		صلاحيات: هدا.ردخاصية('صلاحيات')
	}؛
	
	يملك وضيفة نقرخياتعديل = (هدا._نقرخياتعديل ك وضيفة).ئربط(هدا)؛
	يملك وضيفة نقرخياحدف = (هدا._نقرخياحدف ك وضيفة).ئربط(هدا)؛
تم

ئعلن عنصرمستخدم.نقرخيا ():
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.خدحالة({
		جسم: 'طفخيا'
	})؛
	طفخ.ئضفمستمعحدت('نقرتعديل'، هدا.نقرخياتعديل)؛
	طفخ.ئضفمستمعحدت('نقرحدف'، هدا.نقرخياحدف)؛
تم

ئعلن عنصرمستخدم._نقرخياحدف():
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.ئحدفمستمعحدت('نقرحدف'، هدا.نقرخياحدف)؛
	طفخ.ئخفي()؛
	
	طافية طفنعلا = المستند.ئستعلم('#طفنعملا')؛
	طفنعلا.خدحالة({
		جسم: 'طفنعملا'،
		بيا: {
			رسالة: 'هل أنت متأكد من رغبتك في حذف هذا المستخدم؟'،
			عندلا: (دالة(): طفنعلا.ئخفي())،
			عندنعم: هدا.نقرتئكيدحدف،
			مرجع: هدا
		}
	})؛
تم

ئعلن عنصرمستخدم.نقرتئكيدحدف () ك وعد:
	منطق نتج = ريتما مجالمستخدم.ئحدفبمعرف(هدا.حالة.معرفمستخدم)؛
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.ئخفي()؛
	
	غمازة غمز = المستند.ئستعلم((<غمازة>))؛
	
	ئدا نتج:
		غمز.خدحالة({
			رسالة: 'تم حذف المستخدم'
		})؛
		هدا.ئسمصنف = هدا.ئسمصنف + ' بلاعرض:هام'؛
	وئلا:
		غمز.خدحالة({
			رسالة: 'يتعذر حذف صاحب المتجر'
		})؛
	تم
تم

ئعلن عنصرمستخدم._نقرخياتعديل ():
	طافية طفخ = المستند.ئستعلم('#طفخيا')؛
	طفخ.ئحدفمستمعحدت('نقرتعديل'، هدا.نقرخياتعديل)؛
	طفخ.ئخفي()؛
	طافية طف = المستند.ئستعلم('#طفتعديلمستخدم')؛
	طف.خدحالة({
		جسم: 'طفمستخدم'،
		بيا: {
			معرفمستخدم: هدا.حالة.معرفمستخدم،
			عنصرمستخ: هدا
		}
	})؛
تم

ئعلن عنصرمستخدم.صيير ():
	ئرجع (
		<قسم صنف="مخطط-طولي فجوة-1 حش-2 تئشير:ئطار-ئسود ئطار-2 ئطار-ئبيض خلف-ئبيض مقوس">
			<ئيقو صنف="نصكج3" ئسم="دائرة-ملخاص"></ئيقو>
			<قسم صنف="مخطط-عرضي فجوة-1 ئرصف-بين ئصفف-نهاية">
				<قسم صنف="">
					<حيز صنف="حشتح-2 بلوك">%{هدا.حالة.ئسمكامل}</حيز>
					<حيز صنف="نصصج خلف-رمادي-300 حش-1 مقوس">%{هدا.حالة.صلاحيات}</حيز>
				</قسم>
				<ئيقو عندنقر="نقرخيا" ئسم="تلاتنقط-ئفقي-دائرة" صنف="نصكجج حيادي-800 نفسه-نهاية"></ئيقو>
			</قسم>
		</قسم>
	)؛
تم

ئعلن مدخل ():
	عنصرمخصص.سججل('عنصرمستخدم'، عنصرمستخدم)؛
تم
