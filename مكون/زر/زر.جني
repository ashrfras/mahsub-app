ئورد مكون، عنصرمخصص، حدت؛
ئورد حلقتحميل من مكون.حلقتحميل؛
ئعلن زر ()؛
ئعلن زر.صيير ()؛
ئعلن زر.عندنقرزر (هدف، حدت حت)؛
ئعلن مدخل ()؛
ئعلن زر ():
	يمدد مكون()؛
	يملك منطق عادي،
		منطق تانوي،
		نصية قيمة،
		منطق مفرغ،
		نصية عنونت،
		نصية نبرة؛
		
	يملك مركب { منطق معطل، منطق يحممل } حالة = {
		معطل: هدا.لهخاصية('معطل')،
		يحممل: خطئ
	}؛
	
	هدا.عادي = هدا.لهخاصية("عادي")؛
    هدا.تانوي = هدا.لهخاصية("تانوي")؛
    هدا.قيمة = هدا.ردخاصية("قيمة")؛
    هدا.مفرغ = هدا.لهخاصية("مفرغ")؛
    هدا.عنونت = هدا.ردخاصية("عنونت")؛
	هدا.نبرة = هدا.ردخاصية('نبرة') ئو 'ئسود'؛
تم
ئعلن زر.صيير ():
	نصية لونين = ''؛
	نصية نبرة = هدا.نبرة؛
	نصية مئشر = 'سبابة'؛
	
	نصية[] مقسم = نبرة.قسسم('-')؛
	عدد كمنبرة = مقسم.ئسحب() ك عدد ئدا مقسم.طول > 1 وئلا 800؛
	نصية نصنبرة = 'ئبيض ئتشير:ئبيض' ئدا كمنبرة > 400 وئلا 'ئسود تئشير:ئسود'؛
	
	نصية سهم = '&larr;'؛
	ئدا هدا.تانوي:
		لونين = 'ئطار1 ئطار-حيادي-900 حيادي-900 خلف-ئبيض تئشير:خلف-حيادي-900 تئشير:ئبيض داكن:ئطار-حيادي-200 داكن:حيادي-200 داكن:خلف-حيادي-900 داكن:تئشير:خلف-حيادي-600'
			ئدا هدا.مفرغ وئلا 'خلف-حيادي-700 داكن:خلف-حيادي-600 ئبيض'؛	
	وئلا:
		لونين = "ئطار1 ئطار-%{نبرة} خلف-شفاف %{نبرة} تئشير:%{نصنبرة} تئشير:خلف-%{نبرة} داكن:تئشير:خلف-%{نبرة} داكن:خلف-شفاف"
			ئدا هدا.مفرغ وئلا "خلف-%{نبرة} %{نصنبرة}"؛
	تم
	ئدا هدا.حالة.معطل ئو هدا.حالة.يحممل:
		لونين = 'خلف-رمادي-200 نص-رمادي-600 شفافية-90'؛
		مئشر = 'مئشر-مفترض'؛
	تم
	ئدا ليس هدا.عنونت:
		ئرجع (
			<قسم عندنقر="عندنقرزر" صنف="ع-محتوا ط-كامل تئشير:شفافية-90 مخطط-عرضي ئرصف-بين ئصفف-وسط %{مئشر} حشس-4 حشص-3 %{لونين} %{هدا.ئسمصنف}">
				<شرط>
					<عند تحقق="هدا.حالة.يحممل">
						<حلقتحميل حجم="14" صنف="حشنه-7"></حلقتحميل>
					</عند>
					<عند غيره>
						<حيز ترجم صنف="حشنه-6">%{هدا.قيمة}</حيز>
					</عند>
				</شرط>
				<شرط>
					<عند تحقق="ليس هدا.عادي">
						<حيز صنف="خطسميك">%{سهم}</حيز>
					</عند>
				</شرط>
			</قسم>
		)؛
	وئلا:
		ئرجع (
			<رابط عنونت="%{هدا.عنونت}" صنف="ع-محتوا ط-كامل تئشير:شفافية-90 مخطط-عرضي ئرصف-بين ئصفف-وسط %{مئشر} حشس-4 حشص-3 %{لونين} %{هدا.ئسمصنف}">
				<حيز ترجم صنف="حشنه-6">%{هدا.قيمة}</حيز>
				<شرط>
					<عند تحقق="ليس هدا.عادي">
						<حيز صنف="خطسميك">%{سهم}</حيز>
					</عند>
				</شرط>
			</رابط>
		)؛
	تم
تم
ئعلن زر.عندنقرزر (هدف، حدت حت):
	ئدا هدا.حالة.معطل ئو هدا.حالة.يحممل:
		حت.ئوقفئنتشار()؛
	تم
تم
ئعلن مدخل ():
	عنصرمخصص.سججل('زر'، زر)؛
تم